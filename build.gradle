plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
}

def TEAM = 687
def ROBOT_CLASS = "frc.team687.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', edu.wpi.first.gradlerio.frc.FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Phoenix (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()
    compile name: 'Jama-1.0.3'
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}

import org.apache.tools.ant.taskdefs.condition.Os
task build_matrices(type:Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)){
        commandLine 'cmd', '/C', '.\\run_py.cmd', '.\\src\\main\\python\\robot\\write_gains.py'
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        commandLine 'bash', './run_py.sh', './src/main/python/robot/write_gains.py'
    }
}

task sim_stuff(type:Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)){
        commandLine 'cmd', '/C', '.\\run_py.cmd', '.\\src\\main\\python\\robot\\motor_test.py'
        // commandLine 'cmd', '/C', '.\\run_py.cmd', '.\\src\\main\\python\\robot\\flywheel_test.py'
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        // commandLine 'bash', './run_py.sh', './src/main/python/robot/motor_test.py'
        commandLine 'bash', './run_py.sh', './src/main/python/robot/flywheel_test.py'
    }
}